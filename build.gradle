/*
 * Copyright 2015 Karel H�bl <karel.huebl@gmail.com>.
 *
 * This file is part of disl.
 *
 * Disl is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Disl is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with Foobar.  If not, see <http://www.gnu.org/licenses/>.
 */

 plugins {
	 id "com.github.hierynomus.license" version "0.11.0"
 }
 
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'java-library-distribution'
apply plugin: 'maven'

project.group='com.drmax'
project.version="1.0"

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories { mavenCentral() }

dependencies {
	compile 'org.codehaus.groovy:groovy-all:2.3.10'
	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'junit:junit:4.12'
	testCompile 'org.hsqldb:hsqldb:2.3.2'
	testRuntime 'org.slf4j:slf4j-simple:1.7.21'
	testRuntime fileTree(dir: 'lib', include: '*.jar')
}

license { 
	header rootProject.file('HEADER.txt')
	ext.year = Calendar.getInstance().get(Calendar.YEAR)
	skipExistingHeaders = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier 'sources'
	from sourceSets.main.allSource
}

groovydoc {
	//exclude files causing groovydoc to fail
	exclude 'org/disl/db/oracle/OracleReverseEngineeringService.groovy'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
	classifier = 'javadoc'
	from groovydoc
}
 
artifacts {
	archives groovydocJar, sourcesJar
}

uploadArchives {
	repositories {
		mavenDeployer {
			snapshotRepository(url: "https://apps.prod.dwh.drmax.global/nexus/repository/dwh-drmax-snapshot/") {
				authentication(userName: drmaxNexusUser, password: drmaxNexusPassword)
			}
			repository(url: "https://apps.prod.dwh.drmax.global/nexus/repository/dwh-drmax-release/") {
				authentication(userName: drmaxNexusUser, password: drmaxNexusPassword)
			}
		
			pom.project {
				name 'disl'
				description 'disl = data integration specific language. Goal of this project is to implement groovy based domain specific language supporting modelling of data integration projects. Disl will support data modeling and data mapping including support for MDA transformations and unit testing.'
				url 'https://github.com/drmaxgit/disl'
				licenses {
					license {
						name 'GNU GENERAL PUBLIC LICENSE, Version 3.0'
						url 'http://www.gnu.org/licenses/gpl.txt'
						distribution 'repo'
					}
				}
				developers {
					developer {
						name 'Karel H�bl'
						email 'karel.huebl@gmail.com'
						organization 'Karel H�bl'
						organizationUrl 'https://github.com/kaja78'						
					}
				}
				issueManagement {
					system 'JIRA'
					url 'https://mydrmax.atlassian.net/secure/RapidBoard.jspa?projectKey=DWHAGILE'
				}
				scm {
					connection 'scm:git:git@github.com:kaja78/disl.git'
					developerConnection 'scm:git:git@github.com:kaja78/disl.git'
					url 'git:git@github.com:kaja78/disl.git'
				}
			}
		}
	}
}